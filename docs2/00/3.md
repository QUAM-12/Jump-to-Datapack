# 00-3 선택자
선택자(`target selector`)란 `UUID`나 플레이어 이름을 지정할 필요 없이,
하나 이상의 엔티티나 플레이어를 지정할 수 있습니다.

<br/>

# 종류
`Minecraft Java Edition`에서 사용할 수 있는 선택자는 총 6개입니다.

| 값 | 선택 |
|----|------|
| @p | 가장 가까운 플레이어 |
| @a | 모든 플레이어 |
| @r | 무작위 플레이어 |
| @s | 현재 [[00-1 컨텍스트(context)](00-1)]의 엔티티 |
| @e | 모든 엔티티 |
| @n | 가장 가까운 엔티티 |

---

<br/>

# 인수
선택자에서 특정 엔티티 혹은 플레이어를
필터링 하기 위해 인수를 사용할 수 있습니다.

## 동작
인수 조건에 대한 상세한 동작은 [[00-3-1 인수 상세 동작](00-3-1)]을 참고하십시오.

## 종류
`Minecraft Java Edition`에서 사용할 수 있는 선택자 인수는 총 17개입니다.

| 값 | 조건 |
|----|------|
| x, y, z | 좌표 |
| distance | 거리 |
| dx, dy, dz | 사각형 범위 |
| x_rotation | pitch |
| y_rotation | yaw |
| scores | 점수 |
| tag | 태그 |
| team | 팀 이름 |
| name | 이름 |
| type | 유형 |
| predicate | 술어 |
| nbt | nbt |
| level | 레벨 |
| gamemode | 게임 모드 |
| advancements | 발전과제 |
| limit | 개수 |
| sort | 정렬 |

<br/>

## x, y, z
- 지정: `double`

[[00-2 위치와 방향](00-2)]과 다르게 `x=0`은 `x=0.5`로 보정되지 않습니다.

차원을 제한합니다.

오버월드에 있는 엔티티만 선택합니다.
```mcfunction
execute in minecraft:overworld if entity @e[x=0]
```

<br/>

## distance
- 지정: `min(double) ~ max(double)`
- 오류: 거리는 음수일 수 없음

구 형태로 거리를 계산합니다.

차원을 제한합니다.
```mcfunction
# 10 블록 이상
execute if entity @e[distance=10..]


# 10 블록 이하
execute if entity @e[distance=..10]


# 10 블록
execute if entity @e[distance=10]


# 8 블록 이상 16 블록 이하
execute if entity @e[distance=8..16]
```

<br/>

## dx, dy, dz
- 지정: `double`

직육면체 형태로 엔티티의 히트박스가 포함되는지 확인합니다.
하나의 인수(dx=0, dy=null, dz=null)만 있는 경우 0으로 취급합니다.

차원을 제한합니다.
```mcfunction
# 1x1x1 직육면체
execute if entity @e[dx=0]


# 1x2x1 직육면체
execute if entity @e[dx=0,dy=1]
```

<br/>

## x_rotation, y_rotation
- 지정: `float`

`x_rotation`은 현재 `pitch`를 확인합니다.
```mcfunction
# -90에서 0
execute if entity @e[x_rotation=-90..0]


# 0 이상
execute if entity @e[x_rotation=0..]
```

`y_rotation`은 현재 `yaw`를 확인합니다.
```mcfunction
# -45에서 45
execute if entity @e[y_rotation=-45..45]


# -135에서 -45
execute if entity @e[y_rotation=-135..-45]
```

<br/>

## scores
- 지정: `min(int) ~ max(int)`

엔티티의 점수를 확인합니다.
```mcfunction
# test 1 이상
execute if entity @e[scores={test=1..}]


# test 5 이하, test1 10 이상
execute if entity @e[scores={test=..5, test1=10..}]
```

<br/>

## tag
- 지정: `string`

엔티티가 가지고 있는 태그를 확인합니다.
```mcfunction
# test 태그
execute if entity @e[tag=test]


# test 태그가 없음
execute if entity @e[tag=!test]


# 태그가 없는 엔티티
execute if entity @e[tag=]


# 태그가 하나라도 있는 엔티티
execute if entity @e[tag=!]
```

<br/>

## team
- 지정: `string`

엔티티가 속한 팀의 이름을 확인합니다.
```mcfunction
# test 팀
execute if entity @e[team=test]


# test 팀에 없음
execute if entity @e[team=!test]


# 아무 팀에도 속하지 않음
execute if entity @e[team=]


# 어떤 팀에 속해 있음
execute if entity @e[team=!]
```

<br/>

## type
- 지정: `ID`, `tag`

엔티티의 유형을 확인합니다.
```mcfunction
# 갑옷 거치대
execute if entity @e[type=minecraft:armor_stand]


# 갑옷 거치대가 아님
execute if entity @e[type=!minecraft:armor_stand]


# 언데드 몹
execute if entity @e[type=#minecraft:undead]


# 보트가 아님
execute if entity @e[type=!#minecraft:boat]
```

<br/>

## predicate
- 지정: `predicate`

엔티티가 해당 술어의 조건을 확인합니다.
```mcfunction
execute if entity @e[predicate=test:entity_test]
```

<br/>

## nbt
- 지정: `nbt`

엔티티의 nbt를 확인합니다.
```mcfunction
execute if entity @e[nbt={OnGround:1b}]


# 인벤토리가 비어 있지 않음
execute if entity @a[nbt=!{Inventory:[]}]
```

<br/>

## level
- 지정: `min(int) ~ max(int)`

플레이어의 레벨을 확인합니다.
```mcfunction
# 레벨 10 이상
execute if entity @e[level=10..]


# 레벨 1 이상, 5 이하
execute if entity @e[level=1..5]
```

<br/>

## gamemode
- 지정: `string`

플레이어의 게임 모드를 확인합니다.
```mcfunction
execute if entity @e[gamemode=creative]


# 크리에이티브 및 관전 모드만
execute if entity @e[gamemode=!adventure,gamemode=!survival]
```

<br/>

## advancements
- 지정: `Map<Advancements, boolean>`

플레이어의 발전과제 성공 여부를 확인합니다.
```mcfunction
execute if entity @e[advancements={minecraft:adventure/adventuring_time=true}]


execute if entity @e[advancements={minecraft:adventure/kill_a_mob=false}]
```

<br/>

## limit
- 지정: `int`
- 오류: 개수는 음수가 될 수 없습니다.

엔티티의 선택 개수를 제한합니다.
```mcfunction
execute if entity @e[limit=1]


execute if entity @e[limit=5]
```

<br/>

## sort
- 지정: `string`

엔티티의 정렬 기준을 변경합니다.
```mcfunction
# 가까운 순
execute if entity @e[sort=nearest]


# 가장 가까이 있는 엔티티
execute if entity @e[limit=1, sort=nearest]


# 멀리 있는 순
execute if entity @e[sort=furthest]
```